cmake_minimum_required(VERSION 3.16) # 3.16 is needed for target_precompile_headers

set(MATH_TARGET ${PROJECT_NAME}_Math)

# This .cmake includes and defines the ${PROJECT_NAME}_SOURCES variable !
# It is generated by the generate_cmake_sources script.
include(CMake/CMakeLists_sources.cmake)

if(${PROJECT_NAME}_BUILD_SHARED_LIB)
	add_definitions(-DMOE_DLL_EXPORT)
	add_library(${MATH_TARGET} SHARED ${${MATH_TARGET}_SOURCES})
else()
	add_library(${MATH_TARGET} STATIC ${${MATH_TARGET}_SOURCES} )
endif()

# Create the source groups for source tree with root at CMAKE_CURRENT_SOURCE_DIR
# to recreate the folder tree with filters within Visual Studio for example.
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${${MATH_TARGET}_SOURCES})

target_link_libraries(${MATH_TARGET}
	PUBLIC ${PROJECT_NAME}_Core
	PUBLIC ${PROJECT_NAME})  # Linking with project's Interface Library allows us to reuse PCH's.

# If using GLM, integrate it in the build (it's a header only library)
if(${PROJECT_NAME}_USE_GLM)
	target_link_libraries(${MATH_TARGET} PUBLIC GLM)
endif()

# Setup boilerplate Monocle target configurations (warning level, LTCG etc.)
monocle_target_compile_config(${MATH_TARGET})