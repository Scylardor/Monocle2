cmake_minimum_required(VERSION 3.16) # 3.16 is needed for target_precompile_headers

set(INPUT_TARGET ${PROJECT_NAME}_Input)
set(SOURCE_DIR .)

# This .cmake includes and defines the ${PROJECT_NAME}_SOURCES variable !
# It is generated by the generate_cmake_sources script.
include(CMake/CMakeLists_sources.cmake)

if(${PROJECT_NAME}_BUILD_SHARED_LIB)
	add_definitions(-DMOE_DLL_EXPORT)
	add_library(${INPUT_TARGET} SHARED ${${INPUT_TARGET}_SOURCES} )
else()
	add_library(${INPUT_TARGET} STATIC ${${INPUT_TARGET}_SOURCES} )
endif()

# Create the source groups for source tree with root at CMAKE_CURRENT_SOURCE_DIR
# to recreate the folder tree with filters within Visual Studio for example.
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${${INPUT_TARGET}_SOURCES})

target_include_directories(${INPUT_TARGET} PRIVATE ${PROJECT_SOURCE_DIR}/source)

target_link_libraries(${INPUT_TARGET}
	PUBLIC ${PROJECT_NAME}_Core
	PRIVATE ${PROJECT_NAME})  # Linking with project's Interface Library allows us to reuse PCH's.

# If using GLFW, integrate it in the build
if(${PROJECT_NAME}_USE_GLFW)
	set(GLFW_DIR ${PROJECT_SOURCE_DIR}/vendor/glfw)

	target_include_directories(${INPUT_TARGET} PUBLIC "${GLFW_DIR}/include")

	target_link_directories(${INPUT_TARGET} PUBLIC ${GLFW_DIR})

	target_link_libraries(${INPUT_TARGET} PUBLIC glfw3)
endif()

# Setup boilerplate Monocle target configurations (warning level, LTCG etc.)
monocle_target_compile_config(${INPUT_TARGET})